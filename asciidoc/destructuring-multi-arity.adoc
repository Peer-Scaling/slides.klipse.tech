= Destructuring and Multi Arity
:encoding: utf-8
:navigation:
:deckjs_transition: horizontal-slide
:deckjs_theme: none
:hardbreaks:


== Agenda

* Destructuring - An elegant syntactic sugar
* Multi arity - functions accepting different number of arguments


== Destructuring a sequence

[%step]
--
Destructuring the first `n` values of a vector:

[source,role="clj"]
----
(def point [5 7])

(let [[x y] point]
  {:x x
   :y y})
----
--

[%step]
--
You can skip some items with `_` (don't care):

[source,role="clj"]
----
(def point [5 6 8 7])

(let [[x _ _ y] point]
  {:x x
   :y y})
----
--

[%step]
--
Splitting a vector into `first` and `rest`:

[source,role="clj"]
----
(def indexes [1 2 3])

(let [[x & more] indexes]
  {:x x :more more})
----
--

[%step]
--
What if you want to keep access to the whole sequence?

[source,role="clj"]
----
(def indexes (range 10))

(let [[x & more :as full-list] indexes]
  {:x x :more more :full-list full-list})
----
--

== Destructuring a map

[%step]
--
Choose local names and provide keys

[source,role="clj"]
----
(def point {:x 5 :y 7})

(let [{the-x :x the-y :y} point]
  (+ the-x  the-y))
----
--

[%step]
--

Most useful piece of destructuring: when you want to create locals with the same names as the keys of the map, using the `:keys` directive:


[source,role="clj"]
----
(def point {:x 5 :y 7})

(let [{:keys [x y]} point]
  (+ x y))
----
--

[%step]
--

As with vectors, you can bind the entire map to a local using the :as directive.

[source,role="clj"]
----
(let [{:keys [x y] :as p} {:x 5 :y 7}]
  p)
----
--

== Exercise: Destructuring

How will you destructure a map that is inside a vector, e.g. `[{:a 1 :b 2}]`?

[source,role="clj"]
----
----

[%step]
--
Solution:

[source,role="clj"]
----
(let [[{:keys [a b]}] [{:a 1 :b 2}]]
  a)
----
--

== Exercise: Destructuring (cont.)

Write a function `foo` that receives a map with 4 keys `:a`, `:b`, `:c` and `:d` where:

* `a` and `b` are required
* `c` is optional with a default value of 10
* `d` is optional with a default value of `{:title "hello"}`
* `e` is optional with a default value of `{:xAxis {:scale "linear"} :yAxis {:scale "linear}}`

[%step]
--
[source,role="clj"]
----
(defn foo [])
----
--

== Destructuring: default values

You can specify default values of a map with the `:or` directive:

[source,role="clj"]
----
(let [{:keys [a b c d] :or {c 10 d nil}} {}]
  c)
----


== Destructuring function arguments on the fly

You can destructure function arguments without writing a `let` statement.

This is extremly useful!

[source,role="clj"]
----
(defn barbar [a [b c] {:keys [x y]}]
  [a b c x y])

(barbar 1 [2 3] {:x 4 :y 7})
----


[%step]
--
With this technique, we can have **__variadic arguments**__:

[source,role="clj"]
----
(defn count-args [& args]
  (count args))

(println (count-args 1 2 3 4))
(println (count-args))
----
--


[%step]
--
Sometimes we want to pass a map - without the enclosing brackets:

[source,role="clj"]
----
(defn foofoo [a [b c] & {:keys [x y]}]
  [a b c x y])

(foofoo 1 [2 3] :x 4 :y 7)
----
--

== Exercise: Poor man multi-arity

Write a function `fooz` that receives three numbers and returns their multiplication - where the 2nd and 3rd number have default values of 10 and 20

[source,role="clj"]
----
(defn fooz [& args])

----


[source,role="clj"]
----
(assert (= (fooz 10 2 3)  60))
(assert (= (fooz 10 2)  (* 10 2 20)))
(assert (= (fooz 10)  (* 10 10 20)))
----





[%step]
--
Solution:

[source,role="clj"]
----
#_(defn fooz [& args]
  (case (count args)
    1 (fooz (first args) 10 20)
    2 (fooz (first args) (second args) 20)
    3 (* (first args) (second args) (nth args 2))
    (throw "bad arity")))
----

--


== Destructuring: Summary with hello world

Let's combine all what we have learned about destructuring:

[source,role="clj"]
----
(require '[clojure.string :as string])

(defn hello-world [& {:keys [language upper-case?] 
                      :or {language :en
                           upper-case? false}}]

  (let [greeting (case language
                   :fr "bonjour monde"
                   :en "hello world")]
    (if upper-case?
      (string/upper-case greeting)
      greeting)))
----


[%step]
--
With no params:

[source,role="clj"]
----
(hello-world)
----


With `:language`:

[source,role="clj"]
----
(hello-world :language :fr) 
----

With `:upper-case?`:

[source,role="clj"]
----
(hello-world :upper-case? true)
----
--

== Destructuring - summary

== The arity of functions

*Definition*: _Arity_ is the number of arguments that a function receives.

Examples:

* *0-ary* (a.k.a nullary) functions (no arguments): `rand`, `#(println "foo")`
* *1-ary* (a.k.a unary) functions (1 argument): `inc`, `first`, `count`, `#(println %1)`
* *2-ary* (a.k.a binary) functions (2 arguments): `map`, `concat`, `#(+ %1 %2)`


[%step]
--
Some functions are _variadic_ i.e. it accepts any number of arguments.

Examples: `+`, `concat`...

[source,role="clj"]
----
(defn my-foo [& args]
 (count args))
----

--

[%step]
--
Some functions have a multi arity. The body of the functions is specified for each number of arguments.
--

== Multi-arity functions

Here is how we define different bodies for different number of arguments:

[source, role="clj"]
----
(defn foo-multi
  ([] 1)
  ([a] (* a a a))
  ([a b] (+ a b))
  ([a b c] (* a b c)))
----

[%step]
--
It is a regular function:
[source, role="clj"]
----
(println (foo-multi))
(println (foo-multi 1 2 3))
----
--


[%step]
--
The nice thing is that you can call a body from another body:

[source, role="clj"]
----
(defn bar-multi
  ([] (bar-multi 1))
  ([a] (bar-multi a 1))
  ([a b] (bar-multi a b 10))
  ([a b c] (* a b c)))
----
--


== Exercise: Multi arity

Write a function `count-multi` that counts the number of arguments it receives:

* without using any conditional like `if`, `when`, `cond` etc...
* without using `count`

[source, role="clj"]
----
(defn count-multi [])
----

[source, role="clj"]
----
(assert (= 0 (count-multi)))
(assert (= 1 (count-multi 1)))
(assert (= 5 (count-multi 1 2 3 4 5)))
(assert (= 10 (apply count-multi (range 10))))
----



[%step]
--
Solution:
[source, role="clj"]
----
#_(defn count-multi
  ([] 0)
  ([a] 1)
  ([a & args] (inc (apply count-multi args))))
----
--

include::klipse.adoc[]